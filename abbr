ABBR_SOURCE="$HOME"/.zsh_abbrs

typeset -gA abbrs

if [ -f "$ABBR_SOURCE" ]; then
  while read k v; do abbrs[$k]="$v"; done < "$ABBR_SOURCE"
fi

function fresh_temp() {
  rm $1 2> /dev/null
  mktemp $1 1> /dev/null
}

function add_local_abbr() {
  abbrs+=("$@")
}

function add_global_abbr() {
  local temp_abbrs="$TMPDIR"abbrArray
  fresh_temp "$temp_abbrs"

  for key value in ${(kv)abbrs}; do
    echo "$key $value" >> "$temp_abbrs"
  done

  mv "$temp_abbrs" "$ABBR_SOURCE"
}

function abbr() {
  add_local_abbr "$@"
  add_global_abbr
}

function expand_abbr() {
  local current_word="${LBUFFER/*[ ,;|&\n\t]/}"
  local expansion=${abbrs[$current_word]}

  if [ $expansion ]; then
    local rest="${LBUFFER%%$current_word}"
    LBUFFER="$rest$expansion"
  fi
  zle self-insert
}

zle -N expand_abbr

bindkey " " expand_abbr
bindkey "^ " magic-space
bindkey -M isearch " " magic-space
bindkey -M isearch "^ " expand_abbr


